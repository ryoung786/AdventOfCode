# 2023 Day 06

```elixir
Mix.install([
  {:req, "~> 0.4.5"},
  {:vega_lite, "~> 0.1.8"},
  {:kino_vega_lite, "~> 0.1.11"}
])

alias VegaLite, as: Vl

defmodule AOC do
  @aoc_session System.fetch_env!("LB_AOC_SESSION")

  def day(day, year) when day in 1..25 do
    headers = [cookie: "session=#{@aoc_session}"]
    req = Req.new(base_url: "https://adventofcode.com/#{year}/day/#{day}", headers: headers)
    input = Req.get!(req, url: "/input").body

    if input =~ "Please don't repeatedly request this endpoint before it unlocks",
      do: {:error, "Day #{day} hasn't been unlocked yet"},
      else: {:ok, %{req: req, input: input}}
  end

  def submit(answer, %{req: req}, part \\ :part_1) when part in [:part_1, :part_2] do
    part = if part == :part_2, do: 2, else: 1

    if !part_already_completed?(part, req) do
      body = Req.post!(req, url: "/answer", form: [level: part, answer: answer]).body

      cond do
        body =~ "That's the right answer" -> {:correct, answer}
        body =~ "your answer is too low" -> {:too_low, answer}
        body =~ "your answer is too high" -> {:too_high, answer}
        :else -> {:incorrect, answer}
      end
    else
      {:already_completed_part, answer}
    end
  end

  defp part_already_completed?(part, req) do
    body = Req.get!(req).body

    cond do
      body =~ "Both parts of this puzzle are complete!" -> true
      body =~ "The first half of this puzzle is complete!" -> part == 1
      :else -> false
    end
  end

  def nums(str) do
    Regex.scan(~r/\d+/, str) |> Enum.map(fn [x] -> String.to_integer(x) end)
  end
end

{:ok, day} = AOC.day(6, 2023)
```

## Day

## Part 1

```elixir
test_input = """
Time:      7  15   30
Distance:  9  40  200
"""
```

```elixir
defmodule P1 do
  def solve(input) do
    input
    |> String.split("\n", trim: true)
    |> Enum.map(&AOC.nums/1)
    |> Enum.zip()
    |> Enum.map(&num_winners/1)
    |> Enum.product()
  end

  def num_winners({time, record}), do: Enum.count(0..time, &(time * &1 - &1 * &1 > record))
end

test_input |> P1.solve()
```

## Part 2

```elixir
defmodule P2 do
  def solve(input) do
    input
    |> time_record_pair()
    |> P1.num_winners()
  end

  def time_record_pair(input) do
    input
    |> String.split("\n", trim: true)
    |> Enum.map(fn str ->
      str |> String.split(" ", trim: true) |> Enum.drop(1) |> Enum.join() |> String.to_integer()
    end)
    |> List.to_tuple()
  end
end

test_input |> P2.solve()
```

<!-- livebook:{"offset":2463,"stamp":{"token":"XCP.XErJJD4cu9Qs3zq5p5EXMXwHLqcQ7CVbaV6gT-HK5Y4oy19h1ijJed-DkSGTFVyn9G8YchdFLwmbqRVFq4bzyzkLj9lHP2G1BWO0YbLRjQn0jC16ov4","version":2}} -->
